
# Honor the DESTDIR env variable:
if (DEFINED ENV{DESTDIR})
    message(STATUS "DESTDIR: $ENV{DESTDIR}")
    set(INSTALL_ROOT_DIR $ENV{DESTDIR}/@CMAKE_INSTALL_PREFIX@)
else()
    set(INSTALL_ROOT_DIR @PYTHON_INSTALL_DIRECTORY@)
endif()

set(cmd_
    @Python3_EXECUTABLE@ @MRPT_PYTHON_BUILD_DIRECTORY@/setup.py 
        install
        --root=${INSTALL_ROOT_DIR}
        --install-lib=@PYTHON_INSTALL_DIR@
        --install-layout=deb
        --single-version-externally-managed
)


string(REPLACE ";" " " _cmd_print "${cmd_}")
message(STATUS "Running: ${_cmd_print}")

execute_process(COMMAND ${cmd_}
    RESULT_VARIABLE result
    WORKING_DIRECTORY @MRPT_PYTHON_BUILD_DIRECTORY@
)

if(result)
    message(FATAL_ERROR "CMake step for installing python modules failed: ${result}")
endif()

# Install .pyi files too:
set(cmd_ ${CMAKE_COMMAND}
    -E copy_directory @MRPT_PYTHON_BUILD_DIRECTORY@/mrpt
    ${INSTALL_ROOT_DIR}/@PYTHON_INSTALL_DIR@/mrpt
)
message(STATUS "Installing .pyi files with: ${cmd_}")
execute_process(COMMAND ${cmd_})
