include(CMakeFindDependencyMacro)

if (TARGET mrpt::@MRPT_MODULE_NAME@)
    return()
endif()

# Hints to help CMake finding my dependencies (and theirs own), as well as
# other future calls to search for other mrpt modules from our parent scope:
# 1: MRPT_DIR cmake var:
if (NOT "${MRPT_DIR}" STREQUAL "")
  list(APPEND CMAKE_PREFIX_PATH "${MRPT_DIR}")
  # If the user defines MRPT_DIR, that path overrides whatever CMake on its own
  # found (this file):
  get_filename_component(this_dir "${CMAKE_CURRENT_LIST_DIR}" ABSOLUTE)
  get_filename_component(user_mrpt_dir "${MRPT_DIR}" ABSOLUTE)
  if ((EXISTS "${MRPT_DIR}/mrpt-@MRPT_MODULE_NAME@-config.cmake") AND (NOT "${this_dir}" STREQUAL "${user_mrpt_dir}"))
    message("mrpt-@MRPT_MODULE_NAME@_DIR was: ${mrpt-@MRPT_MODULE_NAME@_DIR}")
    set(mrpt-@MRPT_MODULE_NAME@_DIR "${MRPT_DIR}" CACHE PATH "Path to mrpt-@MRPT_MODULE_NAME@" FORCE)
    include(${MRPT_DIR}/mrpt-@MRPT_MODULE_NAME@-config.cmake)
    return()
  endif()
endif()
# 2) The current (and parent) path:
cmake_policy(PUSH)
cmake_policy(SET CMP0057 NEW) # cmake>=3.3: Allow if(IN_LIST)

if(NOT "${CMAKE_CURRENT_LIST_DIR}" IN_LIST CMAKE_PREFIX_PATH)
    list(APPEND  CMAKE_PREFIX_PATH "${CMAKE_CURRENT_LIST_DIR}")
    list(REMOVE_DUPLICATES CMAKE_PREFIX_PATH)
endif()
if(NOT "${CMAKE_CURRENT_LIST_DIR}/../" IN_LIST CMAKE_PREFIX_PATH)
    list(APPEND  CMAKE_PREFIX_PATH "${CMAKE_CURRENT_LIST_DIR}/../")
    list(REMOVE_DUPLICATES CMAKE_PREFIX_PATH)
endif()
cmake_policy(POP)

# Prefer config instead of the old FindXXX() files
set(_BCK_CMAKE_FIND_PACKAGE_PREFER_CONFIG ${CMAKE_FIND_PACKAGE_PREFER_CONFIG})
set(CMAKE_FIND_PACKAGE_PREFER_CONFIG TRUE)

# Search for dependencies first:
set(_deps "@ALL_DEPS_LIST@")
foreach(_dep ${_deps}) # NO quotes for the list to be a CMake list!
  if (${_dep}_FOUND)
      continue() # already found
  endif()
  find_dependency(${_dep})
  if(${_dep}_FOUND)
    mark_as_advanced(${_dep}_DIR)
  endif()
endforeach()
@EXTRA_CONFIG_CMDS@

# Restore user's settings:
set(CMAKE_FIND_PACKAGE_PREFER_CONFIG ${_BCK_CMAKE_FIND_PACKAGE_PREFER_CONFIG})
unset(_BCK_CMAKE_FIND_PACKAGE_PREFER_CONFIG)

# Include targets for this library:
include(${CMAKE_CURRENT_LIST_DIR}/mrpt-@MRPT_MODULE_NAME@-targets.cmake)
